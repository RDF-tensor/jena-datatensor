PREFIX dt: <https://w3id.org/rdf-tensor/datatypes#>
PREFIX dtf: <https://w3id.org/rdf-tensor/functions#>
PREFIX dta: <https://w3id.org/rdf-tensor/aggregators#>
PREFIX ocs_papers: <https://w3id.org/ocs/ont/papers/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX fabio: <http://purl.org/spar/fabio/>
PREFIX schema: <http://schema.org/>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>

SELECT ?name1 ?name2 (fn:round(?similarity, 5) AS ?cosineSimilarity)
WHERE {
    {
        SELECT ?author1 ?name1 (dta:avg(?o) AS ?avgEmbedding1)
        WHERE {
            ?paper a fabio:ResearchPaper ;
                ocs_papers:hasWordEmbedding ?o ;
                dcterms:creator ?author1 .

            ?author1 schema:name ?name1 .
        }
        GROUP BY ?author1 ?name1
    }

    {
        SELECT ?author2 ?name2 (dta:avg(?o) AS ?avgEmbedding2)
        WHERE {
            ?paper a fabio:ResearchPaper ;
                ocs_papers:hasWordEmbedding ?o ;
                dcterms:creator ?author2 .
            ?author2 schema:name ?name2 .
        }
        GROUP BY ?author2 ?name2
    }
    
    BIND(dtf:cosineSimilarity(?avgEmbedding1, ?avgEmbedding2) AS ?similarity)
    FILTER (STR(?author1) < STR(?author2) && ?similarity < .99)
}
ORDER BY DESC(?similarity)
LIMIT 20
